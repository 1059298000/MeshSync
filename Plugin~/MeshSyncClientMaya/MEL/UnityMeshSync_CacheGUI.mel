// entry point
global proc UnityMeshSync_CacheGUI()
{
    $paths = `fileDialog2 -fileMode 0 -fileFilter "*.sc"`;
    if(size($paths)==0)
        return;

    global string $Window = "UnityMeshSyncCacheGUI";
    if (`window -exists $Window`)
        deleteUI -window $Window;
    window -t ("Scene Cache") -wh 300 400 $Window;

    $objScope           = `UnityMeshSync_Cache -q -objectScope`;
    $frameRange         = `UnityMeshSync_Cache -q -frameRange`;
    $frameBegin         = `UnityMeshSync_Cache -q -frameBegin`;
    $frameEnd           = `UnityMeshSync_Cache -q -frameEnd`;
    $materialFrameRange = `UnityMeshSync_Cache -q -materialFrameRange`;
    $samplesPerFrame    = `UnityMeshSync_Cache -q -samplesPerFrame`;

    $removeNamespace    = `UnityMeshSync_Cache -q -removeNamespace`;
    $zstdCompressionLevel = `UnityMeshSync_Cache -q -zstdCompressionLevel`;
    $makeDoubleSided    = `UnityMeshSync_Cache -q -makeDoubleSided`;
    $bakeDeformers      = `UnityMeshSync_Cache -q -bakeDeformers`;
    $flattenHierarchy   = `UnityMeshSync_Cache -q -flattenHierarchy`;
    $stripNormals       = `UnityMeshSync_Cache -q -stripNormals`;
    $stripTangents      = `UnityMeshSync_Cache -q -stripTangents`;
    //$mergeMeshes        = `UnityMeshSync_Cache -q -mergeMeshes`;

    columnLayout -columnOffset "left" 5;

    rowLayout -nc 2;
        text -l "Path"; textField -text $paths[0] -w 220 umsCtrlCPath;
    setParent ..;

    separator -h 20 -w 250;

    optionMenu -l "Object Scope" umsCtrlCObjectScope;
        menuItem -l "All";
        menuItem -l "Selected";
    optionMenu -e -sl ($objScope + 1) umsCtrlCObjectScope;

    optionMenu -l "Frame Range" -cc umsCBFrameRange umsCtrlCFrameRange;
        menuItem -l "Single";
        menuItem -l "All";
        menuItem -l "Custom";
    optionMenu -e -sl ($frameRange + 1) umsCtrlCFrameRange;

    columnLayout -columnOffset "left" 20 umsCFoldFrameRange;
        rowLayout -nc 2;
            text -l "Frame Begin"; intField -w 60 -v $frameBegin umsCtrlCFrameBegin;
        setParent ..;
        rowLayout -nc 2;
            text -l "Frame End"; intField -w 60 -v $frameEnd umsCtrlCFrameEnd;
        setParent ..;
    setParent ..;

    optionMenu -l "Material Frame Range" umsCtrlCMFRange;
        menuItem -l "None";
        menuItem -l "One";
        menuItem -l "All";
    optionMenu -e -sl ($materialFrameRange + 1) umsCtrlCMFRange;
    
    rowLayout -nc 2;
        text -l "Samples Per Frame"; floatField -w 60 -v $samplesPerFrame umsCtrlCSamplesPerFrame;
    setParent ..;

    separator -h 20 -w 250;

    rowLayout -nc 2;
        text -l "ZSTD Compression"; intField -w 60 -v $zstdCompressionLevel umsCtrlCZSTDCompression;
    setParent ..;

    checkBox -l "Remove Namespace" -v $removeNamespace umsCtrlCRemoveNamespace;
    checkBox -l "Make Double Sided" -v $makeDoubleSided umsCtrlCMakeDoubleSided;
    checkBox -l "Bake Deformers" -v $bakeDeformers umsCtrlCBakeDeformers;
    checkBox -l "Flatten Hierarchy" -v $flattenHierarchy umsCtrlCFlattenHierarchy;
    checkBox -l "Strip Normals" -v $stripNormals umsCtrlCStripNormals;
    checkBox -l "Strip Tangents" -v $stripTangents umsCtrlCStripTangents;

    separator -h 5 -w 250 -style none;

    button -l "Export" -c "umsCBExportCache";

    setParent ..;

    umsCBFrameRange();
    showWindow $Window;
}

global proc umsCBFrameRange()
{
    $v = `optionMenu -q -sl umsCtrlCFrameRange`;
    $visible = $v==3;
    columnLayout -e -visible $visible umsCFoldFrameRange;
}

global proc umsCBExportCache()
{
    $path               = `textField -q -text umsCtrlCPath`;
    $objectScope        = `optionMenu -q -sl umsCtrlCObjectScope` - 1;
    $frameRange         = `optionMenu -q -sl umsCtrlCFrameRange` -  1;
    $frameBegin         = `intField -q -v umsCtrlCFrameBegin`;
    $frameEnd           = `intField -q -v umsCtrlCFrameEnd`;
    $materialFrameRange = `optionMenu -q -sl umsCtrlCMFRange` - 1;
    $samplesPerFrame    = `floatField -q -v umsCtrlCSamplesPerFrame`;

    $removeNamespace    = `checkBox -q -v umsCtrlCRemoveNamespace`;
    $zstdCompression    = `intField -q -v umsCtrlCZSTDCompression`;
    $makeDoubleSided    = `checkBox -q -v umsCtrlCMakeDoubleSided`;
    $bakeDeformers      = `checkBox -q -v umsCtrlCBakeDeformers`;
    $flattenHierarchy   = `checkBox -q -v umsCtrlCFlattenHierarchy`;
    $stripNormals       = `checkBox -q -v umsCtrlCStripNormals`;
    $stripTangents      = `checkBox -q -v umsCtrlCStripTangents`;

    UnityMeshSync_Cache
        -path $path
        -objectScope $objectScope
        -frameRange $frameRange
        -frameBegin $frameBegin
        -frameEnd $frameEnd
        -materialFrameRange $materialFrameRange
        -samplesPerFrame $samplesPerFrame
        -removeNamespace $removeNamespace
        -zstdCompressionLevel $zstdCompression
        -makeDoubleSided $makeDoubleSided
        -bakeDeformers $bakeDeformers
        -flattenHierarchy $flattenHierarchy
        -stripNormals $stripNormals
        -stripTangents $stripTangents
        ;
}

