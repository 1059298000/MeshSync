global proc UnityMeshSync_GUI()
{
    global string $Window = "UnityMeshSync";
    if (`window -exists $Window`) deleteUI -window $Window;
    window -t ("Unity Mesh Sync") -wh 300 360 $Window;
    
    string $address         = `UnityMeshSync_Settings -q -address`;
    int    $port            = `UnityMeshSync_Settings -q -port`;
    float  $scaleFactor     = `UnityMeshSync_Settings -q -scaleFactor`;
    int    $syncMeshes      = `UnityMeshSync_Settings -q -syncMeshes`;
    int    $syncNormals     = `UnityMeshSync_Settings -q -syncNormals`;
    int    $syncUVs         = `UnityMeshSync_Settings -q -syncUVs`;
    int    $syncColors      = `UnityMeshSync_Settings -q -syncColors`;
    int    $syncBlendShapes = `UnityMeshSync_Settings -q -syncBlendShapes`;
    int    $syncBones       = `UnityMeshSync_Settings -q -syncBones`;
    int    $syncCameras     = `UnityMeshSync_Settings -q -syncCameras`;
    int    $syncLights      = `UnityMeshSync_Settings -q -syncLights`;
    int    $syncAnimations  = `UnityMeshSync_Settings -q -syncAnimations`;
    int    $sampleAnimation = `UnityMeshSync_Settings -q -sampleAnimation`;
    int    $animationSPS    = `UnityMeshSync_Settings -q -animationSPS`;
    int    $autosync        = `UnityMeshSync_Settings -q -autosync`;

    columnLayout -columnOffset "left" 5;
    rowLayout -numberOfColumns 3;
    text -label "Server:Port";
    textField -text $address -w 120 -cc "UnityMeshSync_CBServerAddress" msuCtrlAddress;
    intField -value $port -w 60  -cc "UnityMeshSync_CBServerPort" msuCtrlPort;
    setParent ..;

    separator -h 10 -w 250;
    
    rowLayout -numberOfColumns 2;
    text -label "Scale Factor"; floatField -value $scaleFactor -cc "UnityMeshSync_CBScaleFactor" msuCtrlScaleFactor;
    setParent ..;
    checkBox -label "Sync Meshes" -value $syncMeshes -cc "UnityMeshSync_CBSyncMeshes" msuCtrlSyncMeshes;
        columnLayout -columnOffset "left" 20 msuFoldMeshes;
        checkBox -label "Normals" -value $syncNormals -cc "UnityMeshSync_CBSyncNormals" msuCtrlSyncNormals;
        checkBox -label "UVs" -value $syncUVs -cc "UnityMeshSync_CBSyncUVs" msuCtrlSyncUVs;
        checkBox -label "Colors" -value $syncColors -cc "UnityMeshSync_CBSyncColors" msuCtrlSyncColors;
        checkBox -label "Bones" -value $syncBones -cc "UnityMeshSync_CBSyncBones" msuCtrlSyncBones;
        checkBox -label "Blend Shapes" -value $syncBlendShapes -cc "UnityMeshSync_CBSyncBlendShapes" msuCtrlSyncBlendShapes;
        setParent ..;
    checkBox -label "Sync Cameras" -value $syncCameras -cc "UnityMeshSync_CBSyncCameras" msuCtrlSyncCameras;
    checkBox -label "Sync Lights" -value $syncLights -cc "UnityMeshSync_CBSyncLights" msuCtrlSyncLights;
    checkBox -label "Sync Animations" -value $syncAnimations -cc "UnityMeshSync_CBSyncAnimations" msuCtrlSyncAnimations;
        columnLayout -columnOffset "left" 20 msuFoldAnimations;
        checkBox -label "Sample Animation" -value $sampleAnimation -cc "UnityMeshSync_CBSampleAnimation" msuCtrlSampleAnimation;
            columnLayout -columnOffset "left" 20 msuFoldSampleAnimation;
            rowLayout -numberOfColumns 2;
            text -label "Samples per Second"; intField -w 60 -v $animationSPS -cc "UnityMeshSync_CBSPS" msuCtrlSPS;
            setParent ..;
            setParent ..;
        setParent ..;
    
    separator -h 10 -w 250;
    checkBox -label "Auto Sync" -value $autosync -cc "UnityMeshSync_CBAutoSync" msuCtrlAutoSync;
    button -l "Manual Sync" -c "UnityMeshSync_OnManualSync";
    //    separator -h 10 -w 300;
    //    button -l "Import Unity Scene" -w 100 -c "UnityMeshSync_OnImport" BUTTON_IMPORT;
    setParent ..;
    
    if(!$syncMeshes)
        columnLayout -e -visible 0 msuFoldMeshes;
    if(!$syncAnimations)
        columnLayout -e -visible 0 msuFoldAnimations;

    showWindow $Window;
}

global proc UnityMeshSync_CBServerAddress()
{
    string $v = `textField -q -text msuCtrlAddress`;
    string $ret = `UnityMeshSync_Settings -address $v`;
}

global proc UnityMeshSync_CBServerPort()
{
    int $v = `intField -q -value msuCtrlPort`;
    string $ret = `UnityMeshSync_Settings -port $v`;
}

global proc UnityMeshSync_CBScaleFactor()
{
    float $v = `floatField -q -value msuCtrlScaleFactor`;
    string $ret = `UnityMeshSync_Settings -scaleFactor $v`;
}

global proc UnityMeshSync_CBSyncMeshes()
{
    int $v = `checkBox -q -value msuCtrlSyncMeshes`;
    string $ret = `UnityMeshSync_Settings -syncMeshes $v`;
    columnLayout -e -visible $v msuFoldMeshes;
}

global proc UnityMeshSync_CBSyncNormals()
{
    int $v = `checkBox -q -value msuCtrlSyncNormals`;
    string $ret = `UnityMeshSync_Settings -syncNormals $v`;
}

global proc UnityMeshSync_CBSyncUVs()
{
    int $v = `checkBox -q -value msuCtrlSyncUVs`;
    string $ret = `UnityMeshSync_Settings -syncUVs $v`;
}

global proc UnityMeshSync_CBSyncColors()
{
    int $v = `checkBox -q -value msuCtrlSyncColors`;
    string $ret = `UnityMeshSync_Settings -syncColors $v`;
}

global proc UnityMeshSync_CBSyncBlendShapes()
{
    int $v = `checkBox -q -value msuCtrlSyncBlendShapes`;
    string $ret = `UnityMeshSync_Settings -syncBlendShapes $v`;
}

global proc UnityMeshSync_CBSyncBones()
{
    int $v = `checkBox -q -value msuCtrlSyncBones`;
    string $ret = `UnityMeshSync_Settings -syncBones $v`;
}

global proc UnityMeshSync_CBSyncCameras()
{
    int $v = `checkBox -q -value msuCtrlSyncCameras`;
    string $ret = `UnityMeshSync_Settings -syncCameras $v`;
}

global proc UnityMeshSync_CBSyncLights()
{
    int $v = `checkBox -q -value msuCtrlSyncLights`;
    string $ret = `UnityMeshSync_Settings -syncLights $v`;
}

global proc UnityMeshSync_CBSyncAnimations()
{
    int $v = `checkBox -q -value msuCtrlSyncAnimations`;
    string $ret = `UnityMeshSync_Settings -syncAnimations $v`;
    columnLayout -e -visible $v msuFoldAnimations;
}

global proc UnityMeshSync_CBSampleAnimation()
{
    int $v = `checkBox -q -value msuCtrlSampleAnimation`;
    string $ret = `UnityMeshSync_Settings -sampleAnimation $v`;
    columnLayout -e -visible $v msuFoldSampleAnimation;
}

global proc UnityMeshSync_CBSPS()
{
    int $v = `intField -q -value msuCtrlSPS`;
    string $ret = `UnityMeshSync_Settings -animationSPS $v`;
}

global  proc UnityMeshSync_CBAutoSync()
{
    int $v = `checkBox -q -value msuCtrlAutoSync`;
    string $ret = `UnityMeshSync_Settings -autosync $v`;
}

global proc UnityMeshSync_OnManualSync()
{
    string $ret = `UnityMeshSync_Sync`;
}

global proc UnityMeshSync_OnImport()
{
    string $ret = `UnityMeshSync_Import`;
}
